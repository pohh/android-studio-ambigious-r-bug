buildscript {
	repositories {
		maven {url 'https://maven.google.com'}
		jcenter()
	}
	dependencies {
		classpath 'com.android.tools.build:gradle:2.2.3'
//		classpath 'com.android.tools.build:gradle:3.0.0-alpha3'
	}
}

plugins {
	id 'net.saliman.cobertura' version '2.4.0'
}

ext {
	compileSdkVersion = 24
	buildToolsVersion = '25.0.3'
	minSdkVersion = 15
	targetSdkVersion = 23
}

allprojects {
	repositories {
		jcenter()
	}
}

configurations {
	all*.exclude group: 'com.android.support', module: 'support-v4'
}

// Pre dexing should be disabled on CI servers as it causes additional overhead.  This is a likely
// source of crashes when building.
// http://tools.android.com/tech-docs/new-build-system/tips#TOC-Improving-Build-Server-performance
// on jenkins: ./gradlew clean assemble -PdisablePreDex
project.ext.preDexLibs = !project.hasProperty('disablePreDex')
subprojects {
	project.plugins.whenPluginAdded { plugin ->
		if ("com.android.build.gradle.AppPlugin".equals(plugin.class.name)||
			"com.android.build.gradle.LibraryPlugin".equals(plugin.class.name)) {
			project.android.dexOptions.preDexLibraries = rootProject.ext.preDexLibs
		}
	}
}

